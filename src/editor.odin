package game

import "base:runtime"
import "core:fmt"

import im "deps:odin-imgui"

import "core:c"
import "core:io"
import "core:math"
import "core:math/linalg"
import "core:math/linalg/hlsl"
import "core:mem"
import "core:reflect"
import "core:slice"
import "core:strings"
import "core:unicode"

import b3 "vendor:box3d"

import "gfx"

EDITOR :: #config(EDITOR, true)

when EDITOR {
	Debug_Vis_Flag :: enum {
		Irradiance_Probes,
		Reflection_Probes,
		Physics_Bodies,
	}

	Debug_Vis_Flags :: bit_set[Debug_Vis_Flag;u32]

	Editor_Settings :: struct {
		vis_flags:   Debug_Vis_Flags,
		show_editor: bool,
		show_debug:  bool,
	}

	Editor_State :: struct {
		settings:        Editor_Settings,
		inspector:       Inspector,
		selected_entity: any,
	}

	Editor_Window :: struct {
		name: cstring,
		open: bool,
		draw: proc(),
	}

	editor: Editor_State

	editor_windows := []Editor_Window {
		{"Editor Settings", true, proc() {
				inspector_draw_any(editor.settings)
			}},
		{"Physics", true, proc() {
				inspector_draw_any(game.phys)
			}},
		{"Entities", true, proc() {
				if im.CollapsingHeader("Raw Entities") {
					im.Text("%d live entities across %d allocated slots", game.entity_system.live_count, game.entity_system.slot_count)
					clipper: im.ListClipper
					im.ListClipper_Begin(&clipper, i32(game.entity_system.slot_count))

					for im.ListClipper_Step(&clipper) {
						for i in clipper.DisplayStart ..< clipper.DisplayEnd {
							entity := live_entity_at_index(&game.entity_system, u32(i))
							if entity == nil do continue
							im.Text("entity")
							im.BulletText("id %d", entity.id.index)
							im.BulletText("gen %d", entity.id.generation)
						}
					}
				}

				for subtype_ptr, i in game.entity_system.subtype_storage {
					storage_raw := subtype_ptr.ptr
					size_t := subtype_ptr.type_info.size

					if im.SmallButton(fmt.ctprintf("Clear All %s", reflect.enum_string(i))) {
						runtime.map_clear_dynamic(&storage_raw.sparse, &storage_raw.sparse_map_info)
						storage_raw.dense.len = 0
					}

					im.SameLine()

					if im.TreeNode(
						fmt.ctprintf(
							"%s Entities (num: %d)",
							subtype_ptr.type_info.variant.(runtime.Type_Info_Named).name,
							storage_raw.dense.len,
						),
					) {
						clipper: im.ListClipper
						im.ListClipper_Begin(&clipper, i32(storage_raw.dense.len))

						for im.ListClipper_Step(&clipper) {
							for i in clipper.DisplayStart ..< clipper.DisplayEnd {
								data_ptr := (cast([^]u8)storage_raw.dense.data)[int(i) * size_t:]
								inspector_draw_any({data_ptr, subtype_ptr.type_info.id})
							}
						}
						im.TreePop()
					}
				}
			}},
		{"DDGI", true, proc() {
				im.Checkbox("Update", &game.state.update_ddgi)
				im.InputInt("Atlas debug volume", &game.render_state.ddgi_rp.debug_volume)
				if im.Button("Bake Volumes") {
					for &volume, i in get_entities(DDGIVolume) {
						volume.bake_state = .Warmup
					}
				}

				for &volume, i in get_entities(DDGIVolume) {
					im.PushIDInt(i32(i))
					counts := volume.gpu.grid_counts
					im.SeparatorText(
						fmt.ctprintf("Volume %d (%dx%dx%d, prio %.0f)", i, counts[0], counts[1], counts[2], volume.gpu.priority),
					)
					im.BeginDisabled(volume.bake_state == .Warmup || volume.bake_state == .Accumulate)
					im.EndDisabled()
					inspector_draw_any(volume)
					im.PopID()
				}
			}},
		{
			"Reflection Probes",
			true,
			proc() {
				im.Checkbox("Recapture every frame", &game.state.update_reflections)
				// PushID per probe so widgets don't collide on shared labels when there are multiple.
				for &probe, i in get_entities(ReflectionProbe) {
					im.PushIDInt(i32(i))
					im.SeparatorText(fmt.ctprintf("Probe %d", i))
					if im.Button("Recapture") {
						probe.wants_recapture = true // captures next frame, bypassing the auto gate
					}
					im.SliderFloat("Intensity", &probe.intensity, 0.0, 16.0)
					im.SliderFloat("Blend distance", &probe.blend_distance, 0.01, 8.0)
					im.InputFloat3("Position", &probe.translation)
					im.InputFloat3("Half extents", &probe.half_extents)
					im.PopID()
				}
			},
		},
		{"Environment", true, proc() {
				inspector_draw_any(game.state.environment)
			}},
		{
			"Stats",
			true,
			proc() {
				smooth_alpha: f32 = 0.99

				if game.frame_times_smooth[0] == 0 {
					game.frame_times_smooth = game.frame_times
				} else {
					game.frame_times_smooth = math.lerp(game.frame_times, game.frame_times_smooth, smooth_alpha)
				}

				fields := reflect.enum_field_names(FrameTimeStats)

				im.Text("%4.f FPS", (1 / game.frame_times_smooth[0]) * 1000)
				for ms, i in game.frame_times_smooth {
					text_proc := i == 0 ? im.Text : im.BulletText // kinda cursed but ok
					text_proc("%s %2.2f ms", fmt.ctprint(fields[i]), ms)
				}
			},
		},
	}

	init_editor :: proc() {
		editor.settings.show_debug = true

		io := im.GetIO()

		io.ConfigFlags += {.DockingEnable}

		font_config: im.FontConfig = {}

		// Font bytes belong to the asset arena, which outlives the ImGui context.
		font_config.FontDataOwnedByAtlas = false
		font_config.OversampleH = 6
		font_config.OversampleV = 6
		font_config.GlyphMaxAdvanceX = max(f32)
		font_config.RasterizerMultiply = 1.4
		font_config.RasterizerDensity = 1.0
		font_config.EllipsisChar = max(u16)

		font_config.PixelSnapH = false
		font_config.GlyphOffset = {0.0, -1.0}

		im.FontAtlas_AddFontFromFileTTF(io.Fonts, "assets/fonts/f_segoeui.ttf", 18.0, &font_config)

		font_config.MergeMode = true

		ICON_MIN_FA: u16 : 0xe005
		ICON_MAX_FA: u16 : 0xf8ff

		@(static) FA_RANGES: [3]u16 = {ICON_MIN_FA, ICON_MAX_FA, 0}

		font_config.RasterizerMultiply = 1.0
		font_config.GlyphOffset = {0.0, -1.0}

		im.FontAtlas_AddFontFromFileTTF(io.Fonts, "assets/fonts/f_fa_regular_400.ttf", 14.0, &font_config, slice.as_ptr(FA_RANGES[:]))

		font_config.MergeMode = false

		style := im.GetStyle()

		tone_text_1: im.Vec4 : {0.69, 0.69, 0.69, 1.0}
		tone_text_2: im.Vec4 : {0.69, 0.69, 0.69, 0.8}

		tone_1: im.Vec4 : {0.16, 0.16, 0.18, 1.0}
		// tone_1_b := tone_1 * 1.2
		tone_1_e := tone_1 * 1.2
		tone_1_e_a := tone_1_e
		tone_3: im.Vec4 : {0.11, 0.11, 0.12, 1.0}
		//tone_2: im.Vec4 : {0.12, 0.12, 0.13, 1.0}
		tone_2 := tone_3
		tone_2_b: im.Vec4 = tone_2

		style.Colors[im.Col.Text] = tone_text_1
		style.Colors[im.Col.TextDisabled] = tone_text_2
		style.Colors[im.Col.WindowBg] = tone_1
		style.Colors[im.Col.ChildBg] = tone_2
		style.Colors[im.Col.PopupBg] = tone_2_b
		style.Colors[im.Col.Border] = tone_2
		style.Colors[im.Col.BorderShadow] = {0.0, 0.0, 0.0, 0.0}
		style.Colors[im.Col.FrameBg] = tone_3
		style.Colors[im.Col.FrameBgHovered] = tone_2
		style.Colors[im.Col.FrameBgActive] = tone_3
		style.Colors[im.Col.TitleBg] = tone_2
		style.Colors[im.Col.TitleBgActive] = tone_2
		style.Colors[im.Col.TitleBgCollapsed] = tone_2
		style.Colors[im.Col.MenuBarBg] = tone_2
		style.Colors[im.Col.ScrollbarBg] = tone_3
		style.Colors[im.Col.ScrollbarGrab] = tone_1_e
		style.Colors[im.Col.ScrollbarGrabHovered] = tone_2
		style.Colors[im.Col.ScrollbarGrabActive] = tone_1_e_a
		style.Colors[im.Col.CheckMark] = tone_1_e
		style.Colors[im.Col.SliderGrab] = tone_1_e
		style.Colors[im.Col.SliderGrabActive] = tone_1_e_a
		style.Colors[im.Col.Button] = tone_2
		style.Colors[im.Col.ButtonHovered] = tone_3
		style.Colors[im.Col.ButtonActive] = tone_3
		style.Colors[im.Col.Header] = tone_2
		style.Colors[im.Col.HeaderHovered] = tone_3
		style.Colors[im.Col.HeaderActive] = tone_2
		style.Colors[im.Col.Separator] = tone_2
		style.Colors[im.Col.SeparatorHovered] = tone_3
		style.Colors[im.Col.SeparatorActive] = tone_2
		style.Colors[im.Col.ResizeGrip] = {0.0, 0.0, 0.0, 0.0}
		style.Colors[im.Col.ResizeGripHovered] = {0.0, 0.0, 0.0, 0.0}
		style.Colors[im.Col.ResizeGripActive] = {0.0, 0.0, 0.0, 0.0}
		style.Colors[im.Col.Tab] = tone_2
		style.Colors[im.Col.TabHovered] = tone_1
		style.Colors[im.Col.TabActive] = tone_1
		style.Colors[im.Col.TabUnfocused] = tone_1
		style.Colors[im.Col.TabUnfocusedActive] = tone_1
		style.Colors[im.Col.PlotLines] = tone_1_e
		style.Colors[im.Col.PlotLinesHovered] = tone_2
		style.Colors[im.Col.PlotHistogram] = tone_1_e
		style.Colors[im.Col.PlotHistogramHovered] = tone_2
		style.Colors[im.Col.TableHeaderBg] = tone_2
		style.Colors[im.Col.TableBorderStrong] = tone_2
		style.Colors[im.Col.TableBorderLight] = tone_2
		style.Colors[im.Col.TableRowBg] = tone_2
		style.Colors[im.Col.TableRowBgAlt] = tone_1
		style.Colors[im.Col.TextSelectedBg] = tone_1_e
		style.Colors[im.Col.DragDropTarget] = tone_2
		style.Colors[im.Col.NavHighlight] = tone_2
		style.Colors[im.Col.NavWindowingHighlight] = tone_2
		style.Colors[im.Col.NavWindowingDimBg] = tone_2_b
		style.Colors[im.Col.ModalWindowDimBg] = tone_2_b * 0.5

		style.Colors[im.Col.DockingPreview] = {1.0, 1.0, 1.0, 0.5}
		style.Colors[im.Col.DockingEmptyBg] = {0.0, 0.0, 0.0, 0.0}

		style.WindowPadding = {10.00, 10.00}
		style.FramePadding = {5.00, 5.00}
		style.CellPadding = {2.50, 2.50}
		style.ItemSpacing = {5.00, 5.00}
		style.ItemInnerSpacing = {5.00, 5.00}
		style.TouchExtraPadding = {5.00, 5.00}
		style.IndentSpacing = 10
		style.ScrollbarSize = 15
		style.GrabMinSize = 10
		style.WindowBorderSize = 0
		style.ChildBorderSize = 0
		style.PopupBorderSize = 0
		style.FrameBorderSize = 0
		style.TabBorderSize = 0
		style.WindowRounding = 10
		style.ChildRounding = 5
		style.FrameRounding = 5
		style.PopupRounding = 5
		style.GrabRounding = 5
		style.ScrollbarRounding = 10
		style.LogSliderDeadzone = 5
		style.TabRounding = 5
		style.DockingSeparatorSize = 5
	}

	update_imgui :: proc() {
		scope_stat_time(.Imgui)

		view_projection := get_current_projection_view_matrix()

		bl := im.GetBackgroundDrawList()

		if .Physics_Bodies in editor.settings.vis_flags {
			physics_debug_draw(view_projection, bl)
		}

		if action_just_pressed(.ShowEditorUI) {
			editor.settings.show_editor = !editor.settings.show_editor
		}

		if .Reflection_Probes in editor.settings.vis_flags {
			for &probe in get_entities(ReflectionProbe) {
				reflection_probe_debug_draw_box(&probe)
			}
		}

		im.DockSpaceOverViewport(flags = {.PassthruCentralNode})

		if !editor.settings.show_debug {
			return
		}

		@(static) show_messages := false
		@(static) show_gizmos := false

		debug_win_flags := im.WindowFlags {
			.NoTitleBar,
			.NoResize,
			.NoMove,
			.NoScrollbar,
			.NoSavedSettings,
			.NoMouseInputs,
			.NoBackground,
			.AlwaysAutoResize,
		}

		viewport := im.GetMainViewport()
		padding: f32 = 20
		im.SetNextWindowPos(viewport.WorkPos + padding)

		if im.Begin("Test", flags = debug_win_flags) {
			im.PushStyleColor(.Text, im.GetColorU32ImVec4(im.Vec4{1, 1, 1, 1}))
			for i in 0 ..< 20 {
				im.Text("Helllo!!!!!")
			}
			im.PopStyleColor()
		}
		im.End()

		if !editor.settings.show_editor {
			return
		}

		if im.BeginMainMenuBar() {
			if im.BeginMenu("View") {
				if im.MenuItem("Messages") {
					show_messages = !show_messages
				}
				if im.MenuItem("Gizmos") {
					show_gizmos = !show_gizmos
				}
				im.EndMenu()
			}

			if im.BeginMenu("Windows") {
				for &window in editor_windows {
					if im.MenuItem(window.name, nil, window.open) {
						window.open = !window.open
					}
				}
				im.EndMenu()
			}

			im.EndMainMenuBar()
		}

		for &window in editor_windows {
            if window.open {
                if im.Begin(window.name) {
                    window.draw()
                }
                im.End()
            }
		}


		dl := im.GetForegroundDrawList()
		red := im.GetColorU32ImVec4({1.0, 0.0, 0.0, 1.0})
		green := im.GetColorU32ImVec4({0.0, 1.0, 0.0, 1.0})
		blue := im.GetColorU32ImVec4({0.0, 0.0, 1.0, 1.0})

		player := get_entity(game.state.player_id)
		{
			view_matrix := linalg.matrix4_from_quaternion(player != nil ? player.rotation : {})

			projection_matrix := gfx.matrix_ortho3d_z0_f32(-1, 1, -1, 1, 0.1, 1)
			projection_matrix[1][1] *= -1.0

			view_projection_matrix := view_matrix * projection_matrix

			origin_ws := hlsl.float4{0, 0, 0, 1}

			x_pos_ws := hlsl.float4{1, 0, 0, 1} * 20
			y_pos_ws := hlsl.float4{0, 1, 0, 1} * 20
			z_pos_ws := hlsl.float4{0, 0, 1, 1} * 20

			offset_vs := hlsl.float2{f32(gfx.r_ctx.draw_extent.width) - 30, f32(gfx.r_ctx.draw_extent.height) - 30}

			origin := (origin_ws * view_projection_matrix).xy + offset_vs
			x_pos := (x_pos_ws * view_projection_matrix).xy + offset_vs
			y_pos := (y_pos_ws * view_projection_matrix).xy + offset_vs
			z_pos := (z_pos_ws * view_projection_matrix).xy + offset_vs

			im.DrawList_AddLine(dl, origin, x_pos, red, 2)
			im.DrawList_AddLine(dl, origin, y_pos, green, 2)
			im.DrawList_AddLine(dl, origin, z_pos, blue, 2)
		}
	}

	inspector_label :: proc(label: string) {
		if label != "" {
			im.SameLine()
			im.TextUnformatted(fmt.ctprintf(label))
		}
	}

	to_pretty_case :: proc(
		s: string,
		allocator := context.allocator,
	) -> (
		res: string,
		err: runtime.Allocator_Error,
	) #optional_allocator_error {
		s := s
		s = strings.trim_space(s)
		b: strings.Builder
		strings.builder_init(&b, 0, len(s), allocator) or_return
		w := strings.to_writer(&b)

		strings.string_case_iterator(w, s, proc(w: io.Writer, prev, curr, next: rune) {
			if !strings.is_delimiter(curr) {
				if strings.is_delimiter(prev) || prev == 0 || (unicode.is_lower(prev) && unicode.is_upper(curr)) {
					if prev != 0 {
						io.write_rune(w, ' ')
					}
					io.write_rune(w, unicode.to_upper(curr))
				} else {
					io.write_rune(w, unicode.to_lower(curr))
				}
			}
		})

		return strings.to_string(b), nil
	}

	Inspector_Draw_Proc :: #type proc(base: rawptr) -> (draw_label: bool)

	Inspector :: struct {
		registered_types: map[typeid]Inspector_Draw_Proc,
	}

	register_custom_inspector :: proc(draw: proc(base: ^$T) -> (draw_label: bool)) {
		game.inspector.registered_types[T] = draw
	}

	inspector_draw_any :: proc(value: any) -> bool {
		base := value.data
		type_info := type_info_of(value.id)

		im.PushIDPtr(base)
		defer im.PopID()

		#partial switch &v in type_info.variant {
		case runtime.Type_Info_Integer, runtime.Type_Info_Float:
			if !reflect.is_endian_platform(type_info) {
				return false
			}

			data_type: im.DataType
			speed: f32 = 1.0

			integer_info, is_integer := type_info.variant.(runtime.Type_Info_Integer)

			if is_integer {
				switch type_info.size {
				case 1:
					data_type = integer_info.signed ? .S8 : .U8
				case 2:
					data_type = integer_info.signed ? .S16 : .U16
				case 4:
					data_type = integer_info.signed ? .S32 : .U32
				case 8:
					data_type = integer_info.signed ? .S64 : .U64
				case:
					return false
				}
			} else {
				switch type_info.size {
				case 4:
					data_type = .Float
				case 8:
					data_type = .Double
				case:
					return false
				}
				speed = 0.01
			}

			im.DragScalar("##value", data_type, base, speed)

		case runtime.Type_Info_Boolean:
			edit_value, ok := reflect.as_bool(value)
			assert(ok)
			if im.Checkbox("##value", &edit_value) {
				switch &dst in value {
				case bool:
					dst = edit_value
				}
			}

		case runtime.Type_Info_Enum:
			if len(v.names) == 0 {
				return false
			}

			enum_info := v.base

			for {
				named, ok := enum_info.variant.(runtime.Type_Info_Named)
				if !ok {
					break
				}
				enum_info = named.base
			}

			curr: runtime.Type_Info_Enum_Value
			switch type_info.size {
			case 1:
				curr = runtime.Type_Info_Enum_Value((^u8)(base)^)
			case 2:
				curr = runtime.Type_Info_Enum_Value((^u16)(base)^)
			case 4:
				curr = runtime.Type_Info_Enum_Value((^u32)(base)^)
			case 8:
				curr = runtime.Type_Info_Enum_Value((^u64)(base)^)
			}

			selected: int

			for value, i in v.values {
				if value == curr {
					selected = i
				}
			}

			if im.BeginCombo("##value", fmt.ctprint(to_pretty_case(v.names[selected]))) {
				for name, i in v.names {
					is_selected := i == selected

					if im.Selectable(strings.clone_to_cstring(to_pretty_case(name), context.temp_allocator), is_selected) {
						selected = i
						value := v.values[i]
						switch type_info.size {
						case 1:
							(^u8)(base)^ = u8(value)
						case 2:
							(^u16)(base)^ = u16(value)
						case 4:
							(^u32)(base)^ = u32(value)
						case 8:
							(^u64)(base)^ = u64(value)
						}
					}

					if is_selected {
						im.SetItemDefaultFocus()
					}
				}
				im.EndCombo()
			}

		case runtime.Type_Info_Array:
			if v.count == 0 {
				return false
			}

			is_scalar := reflect.is_float(v.elem) || reflect.is_integer(v.elem)

			if v.count <= 4 && is_scalar {
				im.PushItemWidth(75)

				im.BeginGroup()
				for i in 0 ..< v.count {
					if i != 0 {
						im.SameLine()
					}
					elem_base := rawptr(uintptr(base) + uintptr(i * v.elem_size))
					inspector_draw_any({elem_base, v.elem.id})
				}
				im.EndGroup()
				im.PopItemWidth()
			} else {
				for i in 0 ..< v.count {
					elem_base := rawptr(uintptr(base) + uintptr(i * v.elem_size))
					inspector_draw_any({elem_base, v.elem.id})
				}
			}

		case runtime.Type_Info_Dynamic_Array:
			array := cast(^runtime.Raw_Dynamic_Array)base

			for i in 0 ..< array.len {
				elem_base := rawptr(uintptr(array.data) + uintptr(i * v.elem_size))
				inspector_draw_any({elem_base, v.elem.id})
				inspector_label(fmt.tprint(i))
			}

		case runtime.Type_Info_Struct:
			zipped := soa_zip(
				name = v.names[:v.field_count],
				type = v.types[:v.field_count],
				tag = ([^]reflect.Struct_Tag)(v.tags)[:v.field_count],
				offset = v.offsets[:v.field_count],
				is_using = v.usings[:v.field_count],
			)

			for field in zipped {
				tag_value, has_tag := reflect.struct_tag_lookup(field.tag, "edit")

				if tag_value == "-" {
					continue
				}

				field_base := rawptr(uintptr(base) + field.offset)
				field_value := any{field_base, field.type.id}
				field_info := field.type

				for {
					named, ok := field_info.variant.(runtime.Type_Info_Named)
					if !ok {
						break
					}
					field_info = named.base
				}

				label: string
				if label_value, has_label := reflect.struct_tag_lookup(field.tag, "label"); has_label {
					label = label_value
				} else {
					label = to_pretty_case(field.name)
				}


				if array_info, is_array := field_info.variant.(runtime.Type_Info_Dynamic_Array); is_array {
					array := (^runtime.Raw_Dynamic_Array)(field_base)
					im.PushIDPtr(field_base)

					button_size := im.GetFrameHeight()
					spacing := im.GetStyle().ItemSpacing.x
					count_label := fmt.ctprintf("%d entries", array.len)
					controls_width := im.CalcTextSize(count_label).x + 2 * button_size + 2 * spacing
					controls_x := im.GetCursorPosX() + max(0, im.GetContentRegionAvail().x - controls_width)
					open := im.TreeNodeEx(
						strings.clone_to_cstring(label, context.temp_allocator),
						{.SpanAvailWidth, .AllowOverlap, .FramePadding, .NoTreePushOnOpen},
					)

					im.SameLine(controls_x)
					im.TextUnformatted(count_label)
					im.SameLine()
					if im.Button("+", {button_size, button_size}) {
						zero, alloc_err := mem.alloc(array_info.elem.size, array_info.elem.align, context.temp_allocator)
						if alloc_err == nil {
							appended, append_err := runtime._append_elems(
								array,
								array_info.elem.size,
								array_info.elem.align,
								should_zero = true,
								args = zero,
								arg_len = 1,
							)
							if append_err != nil || appended != 1 {
								fmt.eprintln("Failed to append inspector array element:", append_err)
							}
						} else {
							fmt.eprintln("Failed to allocate inspector array element:", alloc_err)
						}
					}
					im.SameLine()
					im.BeginDisabled(array.len == 0)
					if im.Button("-", {button_size, button_size}) && array.len > 0 {
						array.len -= 1
					}
					im.EndDisabled()

					if open {
						im.TreePush("contents")
						inspector_draw_any(field_value)
						im.TreePop()
					}
					im.PopID()
				} else if _, is_bit_set := field_info.variant.(runtime.Type_Info_Bit_Set); is_bit_set {
					im.PushIDPtr(field_base)
					if im.TreeNodeEx(
						strings.clone_to_cstring(label, context.temp_allocator),
						{.DefaultOpen, .SpanAvailWidth, .FramePadding},
					) {
						inspector_draw_any(field_value)
						im.TreePop()
					}
					im.PopID()
				} else if reflect.is_struct(field_info) {
					if im.TreeNode(strings.clone_to_cstring(label, context.temp_allocator)) {
						inspector_draw_any(field_value)
						im.TreePop()
					}
				} else if inspector_draw_any(field_value) {
					inspector_label(label)
				}
			}

			return false

		case runtime.Type_Info_Bit_Set:
			bytes := ([^]u8)(base)[:type_info.size]
			big_endian := reflect.bit_set_is_big_endian(value)
			im.BeginGroup()
			defer im.EndGroup()

			_draw_bit :: proc(bytes: []u8, bit: i64, big_endian: bool, label: cstring) {
				if bit < 0 || bit >= i64(len(bytes)) * 8 {
					return
				}
				byte_index := int(bit / 8)
				if big_endian {
					byte_index = len(bytes) - 1 - byte_index
				}
				mask := u8(1) << u8(bit % 8)
				checked := bytes[byte_index] & mask != 0
				if im.Checkbox(label, &checked) {
					if checked {
						bytes[byte_index] |= mask
					} else {
						bytes[byte_index] &~= mask
					}
				}
			}

			elem_info := runtime.type_info_base(v.elem)
			if enum_info, is_enum := elem_info.variant.(runtime.Type_Info_Enum); is_enum {
				for name, i in enum_info.names {
					im.PushIDInt(i32(i))
					_draw_bit(
						bytes,
						i64(enum_info.values[i]) - v.lower,
						big_endian,
						strings.clone_to_cstring(to_pretty_case(name), context.temp_allocator),
					)
					im.PopID()
				}
			} else {
				for i in v.lower ..= v.upper {
					_draw_bit(bytes, i - v.lower, big_endian, fmt.ctprint(i))
				}
			}

		case runtime.Type_Info_Named:
			return inspector_draw_any({base, v.base.id})

		case runtime.Type_Info_Pointer:
			if v.elem == nil {
				return false
			}

			if base != nil {
				im.TextUnformatted("nil")
				return true
			}

			dest := (cast(^rawptr)base)^
			return inspector_draw_any({dest, v.elem.id})
		case:
			return false
		}

		return true
	}

	Phys_Debug_Ctx :: struct {
		view_projection: Mat4x4,
		draw_list:       ^im.DrawList,
	}

	phys_hex_to_im :: proc(color: b3.HexColor) -> u32 {
		c := u32(color)
		r := f32((c >> 16) & 0xff) / 255
		g := f32((c >> 8) & 0xff) / 255
		b := f32(c & 0xff) / 255
		return im.GetColorU32ImVec4({r, g, b, 1})
	}

	phys_debug_segment :: proc "c" (p1, p2: b3.Pos, color: b3.HexColor, ctx: rawptr) {
		context = runtime.default_context()
		dc := cast(^Phys_Debug_Ctx)ctx
		a, ok0 := world_space_to_clip_space(dc.view_projection, transmute(Vec3)p1)
		b, ok1 := world_space_to_clip_space(dc.view_projection, transmute(Vec3)p2)
		if !ok0 || !ok1 do return
		im.DrawList_AddLine(dc.draw_list, a, b, phys_hex_to_im(color), 1.0)
	}

	phys_debug_bounds :: proc "c" (aabb: b3.AABB, color: b3.HexColor, ctx: rawptr) {
		context = runtime.default_context()
		dc := cast(^Phys_Debug_Ctx)ctx
		lo := transmute(Vec3)aabb.lowerBound
		hi := transmute(Vec3)aabb.upperBound
		corners := [8]Vec3 {
			{lo.x, lo.y, lo.z},
			{hi.x, lo.y, lo.z},
			{hi.x, hi.y, lo.z},
			{lo.x, hi.y, lo.z},
			{lo.x, lo.y, hi.z},
			{hi.x, lo.y, hi.z},
			{hi.x, hi.y, hi.z},
			{lo.x, hi.y, hi.z},
		}
		edges := [12][2]int{{0, 1}, {1, 2}, {2, 3}, {3, 0}, {4, 5}, {5, 6}, {6, 7}, {7, 4}, {0, 4}, {1, 5}, {2, 6}, {3, 7}}
		col := phys_hex_to_im(color)
		for e in edges {
			a, ok0 := world_space_to_clip_space(dc.view_projection, corners[e[0]])
			b, ok1 := world_space_to_clip_space(dc.view_projection, corners[e[1]])
			if !ok0 || !ok1 do continue
			im.DrawList_AddLine(dc.draw_list, a, b, col, 1.0)
		}
	}

	physics_debug_draw :: proc(view_projection: Mat4x4, draw_list: ^im.DrawList) {
		dc := Phys_Debug_Ctx{view_projection, draw_list}

		dd := b3.DefaultDebugDraw()
		dd.DrawSegmentFcn = phys_debug_segment
		dd.DrawBoundsFcn = phys_debug_bounds
		dd.drawShapes = true
		dd.drawBounds = true
		dd.ctx = &dc

		b3.World_Draw(game.phys.world, &dd, max(u64))
	}
}
