package game

import "core:time"

import glfw "vendor:glfw"

import b3 "vendor:box3d"

import gfx "gfx"

NUM_FRAME_AVG_COUNT :: 10

PhysicsContext :: struct {
	world:          b3.WorldId `edit:"-"`,
	update_physics: bool,
}

ViewState :: enum {
	SceneColor,
	SceneDepth,
	ShadowDepth,
	Raytracing,
}

SkeletalMeshInstance :: struct {
	preskinned_vertex_buffers: [gfx.FRAME_OVERLAP]gfx.Buffer(Vertex),
	joint_matrices_buffers:    [gfx.FRAME_OVERLAP]gfx.Buffer(Mat4x4),
	skel:                      ^Skeleton,
	animator:                  SkeletonAnimator,
}

init_skeletal_mesh_instance :: proc(skel: ^Skeleton, anim: ^SkeletalAnimation) -> SkeletalMeshInstance {
	instance := SkeletalMeshInstance {
		skel = skel,
	}
	init_skinning_instance(&instance, anim)

	return instance
}

Game :: struct {
	initialized:        bool,
	window:             glfw.WindowHandle,
	window_state:       struct {
		is_fullscreen: bool,
		windowed_pos:  [2]i32,
		windowed_size: [2]i32,
	},
	config:             GameConfig,
	state:              GameState,
	renderer:           ^gfx.Renderer,

	// Systems
	entity_system:      EntitySystem,
	input_system:       InputSystem,
	sound_system:       SoundSystem,
	asset_system:       AssetSystem,
	view_state:         ViewState,
	render_state:       RenderState,

	// Physics
	phys:               PhysicsContext,

	// Stats
	frame_times:        [len(FrameTimeStats)]f32,
	frame_times_smooth: [len(FrameTimeStats)]f32,
	frame_times_start:  [len(FrameTimeStats)]time.Tick,
	frame_time_start:   time.Tick,
	delta_time:         f64,
	live_time:          f64,

	// TEMP storage
	ball_mesh:          GPUMeshBuffers,
}

FrameTimeStats :: enum {
	Total,
	GameState,
	Imgui,
	Physics,
	Render,
}

@(deferred_in = end_scope_stat_time)
scope_stat_time :: proc(stat_type: FrameTimeStats) {
	start_scope_stat_time(stat_type)
}

start_scope_stat_time :: proc(stat_type: FrameTimeStats) {
	game.frame_times_start[stat_type] = time.tick_now()
}

end_scope_stat_time :: proc(stat_type: FrameTimeStats) {
	game.frame_times[stat_type] = f32(time.tick_since(game.frame_times_start[stat_type])) / f32(time.Millisecond)
}

GameState :: struct {
	current_scene:      Scene,
	environment:        Environment,
	player_id:          TypedEntityId(Player),
	update_ddgi:        bool,
	update_reflections: bool,
}

Environment :: struct {
	atmosphere:    AtmosphereSettings,
	sun_color:     Vec3,
	sky_color:     Vec3,
	sun_direction: Vec3,
}
